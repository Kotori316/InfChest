import java.nio.file.Files
import java.nio.file.Paths
import com.kotori316.plugin.cf.CallVersionCheckFunctionTask
import com.kotori316.plugin.cf.CallVersionFunctionTask

plugins {
    id("maven-publish")
    id("signing")
    // https://maven.fabricmc.net/net/fabricmc/fabric-loom/
    id("fabric-loom") version("1.2-SNAPSHOT")
    id("com.matthewprenger.cursegradle") version("1.4.0")
    id("com.modrinth.minotaur") version("2.+")
    id("com.kotori316.plugin.cf") version("2.+")
}

archivesBaseName = "${project.baseName}-Fabric-${project.minecraftVersion}"
version = project.modVersion
group = "com.kotori316"

println("Java: " + System.getProperty("java.version") +
        " JVM: " + System.getProperty("java.vm.version") +
        "(" + System.getProperty("java.vendor") + ")" +
        " Arch: " + System.getProperty("os.arch"))

loom {
    runs {
        client {
            configName = "Client"
            runDir = "run"
        }
        gameTest {
            configName = "GameTest"
            server()
            //noinspection SpellCheckingInspection
            vmArgs("-ea", "-Dfabric-api.gametest", "-Dfabric-api.gametest.report-file=game_test/test-results/test/game_test.xml")
            runDir = "game_test"
            source sourceSets.test
        }
    }
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "What The Hell Is That"
        url = "https://maven2.bai.lol"
        content {
            it.includeVersion("mcp.mobius.waila", "wthit-api", "fabric-${project.wthit_fabric_version}")
            it.includeVersion("mcp.mobius.waila", "wthit", "fabric-${project.wthit_fabric_version}")
            it.includeVersion("lol.bai", "badpackets", "fabric-${project.badpackets_fabric_version}")
        }
    }
    maven {
        url = "https://www.cursemaven.com"
        content {
            it.includeGroup("curse.maven")
        }
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev/"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings loom.layered {
        it.officialMojangMappings()
        String[] parchmentVersion = String.valueOf(project.parchmentMapping).split("-", 2)
        String parchmentMC = parchmentVersion[0]
        String parchmentDate = parchmentVersion[1]
        it.parchment("org.parchmentmc.data:parchment-$parchmentMC:$parchmentDate@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    compileOnly project(":common")
    testCompileOnly project(":common")

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation("appeng:appliedenergistics2-fabric:${project.ae2_fabric_version}") {
        transitive(false)
    }
    //noinspection SpellCheckingInspection
    modRuntimeOnly("teamreborn:energy:3.0.0") // For AE2
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_fabric_version}")
    // modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${project.wthit_fabric_version}")
    // modRuntimeOnly("lol.bai:badpackets:fabric-${project.badpackets_fabric_version}")
    modImplementation("curse.maven:jade-324717:${project.jade_fabric_id}")
}

processResources {
    from project(":common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    source(project(":common").sourceSets.main.allSource)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

curseforge {
    apiKey = project.findProperty("curseforge_additional-enchanted-miner_key") ?: System.getenv("CURSE_TOKEN") ?: ""
    project {
        id = '312222'
        changelogType = 'markdown'
        changelog = file("../temp_changelog.md")
        addGameVersion 'Fabric'
        addGameVersion project.minecraftVersion
        releaseType = 'beta'
        mainArtifact remapJar
    }
    options {
        curseGradleOptions.debug = !Boolean.parseBoolean(project.releaseMode)
        curseGradleOptions.javaVersionAutoDetect = false
        curseGradleOptions.forgeGradleIntegration = false
    }
}

modrinth {
    token.set((project.findProperty("modrinthToken") ?: System.getenv("MODRINTH_TOKEN") ?: "") as String)
    projectId = "infchest"
    versionType = "release"
    versionName = "${project.version}-fabric"
    versionNumber.set(project.version.toString())
    uploadFile = tasks.remapJar
    getGameVersions().set([project.minecraftVersion])
    getLoaders().set(["fabric"])
    changelog = file("../temp_changelog.md").text.split("# ")[1]
    debugMode = !Boolean.parseBoolean(project.releaseMode)
    dependencies {
    }
}

publishing {
    if (Boolean.parseBoolean(project.releaseMode)) {
        repositories {
            maven {
                name = "AzureRepository"
                url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
                credentials {
                    username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                    password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName.toLowerCase()
            from components.java
        }
    }
}

tasks.register('checkOutput') {
    doLast {
        [remapJar, jar, sourcesJar].forEach { t ->
            println("$t -> ${t.getArchiveFile().get().asFile}")
        }
        println("$remapSourcesJar -> ${remapSourcesJar.output}")
    }
}

tasks.register('copyToDrive', Copy) {
    dependsOn("build")
    def PATH_NAME = "drive_path"
    from(remapJar.getArchiveFile())
    into(file(java.util.Optional.ofNullable(System.getenv(PATH_NAME)).orElse(".")))
    onlyIf {
        System.getenv(PATH_NAME) != null &&
                Files.exists(Paths.get(System.getenv(PATH_NAME)))
    }
}

remapJar.finalizedBy("jksSignJar")

tasks.register("jksSignJar") {
    dependsOn("remapJar")
    boolean executeCondition = project.hasProperty("jarSign.keyAlias") &&
            project.hasProperty("jarSign.keyLocation") &&
            project.hasProperty("jarSign.storePass")
    onlyIf { executeCondition }
    doLast {
        //noinspection HttpUrlsUsage
        ant.signjar(
                jar: remapJar.archiveFile.get(),
                alias: project.findProperty("jarSign.keyAlias") ?: "",
                keystore: project.findProperty("jarSign.keyLocation") ?: "",
                storepass: project.findProperty("jarSign.storePass") ?: "",
                sigalg: "Ed25519",
                digestalg: "SHA-256",
                tsaurl: "http://timestamp.digicert.com",
        )
    }
}

signing {
    sign(publishing.publications)
    sign(tasks.jar, tasks.remapJar, tasks.sourcesJar)
}

boolean hasGpgSignature = project.hasProperty("signing.keyId") &&
        project.hasProperty("signing.password") &&
        project.hasProperty("signing.secretKeyRingFile")

tasks.withType(Sign).configureEach {
    it.onlyIf {
        hasGpgSignature
    }
}

tasks.withType(AbstractPublishToMaven).configureEach {
    if (hasGpgSignature) {
        it.dependsOn(":fabric:signRemapJar")
    }
}

tasks.register("registerVersion", CallVersionFunctionTask.class) {
    functionEndpoint = CallVersionFunctionTask.readVersionFunctionEndpoint(project)
    gameVersion = project.minecraftVersion as String
    platform = "fabric"
    modName = "infchest"
    changelog = "For ${project.minecraftVersion}"
    isDryRun = false
    homepage = "https://www.curseforge.com/minecraft/mc-mods/infchest"
}

tasks.register("checkReleaseVersion", CallVersionCheckFunctionTask.class) {
    gameVersion = project.minecraftVersion as String
    platform = "fabric"
    modName = "infchest"
    version = project.version as String
    failIfExists = true
}
