import java.nio.file.Files
import java.nio.file.Paths
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    // https://maven.fabricmc.net/net/fabricmc/fabric-loom/
    id 'fabric-loom' version "0.11-SNAPSHOT"
    id 'maven-publish'
    //noinspection SpellCheckingInspection
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

version = project.mod_version
group = "com.kotori316"
archivesBaseName = "InfChest"

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    toolchain {
        it.languageVersion = JavaLanguageVersion.of(17)
    }
}

loom {
    runs {
        client {
            configName = "Client"
            runDir = "Minecraft"
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

sourceSets {
    main {
        java {
            srcDir 'scala'
        }
        resources {
            srcDir 'resources'
        }
    }
}

repositories {
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev/"
    }
    maven {
        name = "What The Hell Is That"
        url = "https://maven.bai.lol"
        content {
            it.includeVersion("mcp.mobius.waila", "wthit-api", "fabric-${project.wthit_version}")
            it.includeVersion("mcp.mobius.waila", "wthit", "fabric-${project.wthit_version}")
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // FIXME wait until AE2 supports 1.18.2
    modCompileOnly("appeng:appliedenergistics2-fabric:${project.ae2_version}") {
        transitive(false)
    }
    //noinspection SpellCheckingInspection
    modRuntimeOnly("teamreborn:energy:2.2.0") // For AE2
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}")
    modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${project.wthit_version}")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Kotori316",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion.get(),
                "Implementation-Vendor"   : "Kotori316",
                "Implementation-Timestamp": ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT)
        ])
    }
}

curseforge {
    apiKey = project.hasProperty("curseforge_additional-enchanted-miner_key") ? getProperty("curseforge_additional-enchanted-miner_key") : ""
    project {
        id = '312222'
        changelogType = 'markdown'
        changelog = file("temp_changelog.md")
        addGameVersion 'Fabric'
        addGameVersion '1.18.1'
        releaseType = 'beta'
        mainArtifact remapJar
    }
    options {
        curseGradleOptions.debug = false
        curseGradleOptions.javaVersionAutoDetect = false
        curseGradleOptions.forgeGradleIntegration = false
    }
}

publishing {
    repositories {
        maven {
            name = "AzureRepository"
            url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
            credentials {
                username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = "InfChest".toLowerCase()
            from components.java
        }
    }
}

task checkOutput() {
    doLast {
        [remapJar, jar, sourcesJar].forEach { t ->
            println("$t -> ${t.getArchiveFile().get().asFile}")
        }
        println("$remapSourcesJar -> ${remapSourcesJar.output}")
    }
}

task copyToDrive(type: Copy, dependsOn: "build") {
    def PATH_NAME = "drive_path"
    from(remapJar.getArchiveFile())
    into(file(java.util.Optional.ofNullable(System.getenv(PATH_NAME)).orElse(".")))
    onlyIf {
        System.getenv(PATH_NAME) != null &&
                Files.exists(Paths.get(System.getenv(PATH_NAME)))
    }
}
